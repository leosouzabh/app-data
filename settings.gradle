include 'modules:ta-castor'
include 'modules:ta-services2'
include 'modules:ta-core'
include 'modules:ta-garage'
include 'modules:ta-test'
include 'modules:ta-ddlgenerator'
include 'modules:ta-bom'
//include 'webapps:ta-garage-war'
include 'docs'

/*
    The LessCSS plugin we are using caused our build time to increase from <2 mins to 10+ mins
    This plugin included an explicit repository reference to MavenCentral() in its source code
    which had the effect of forcing all of our dependency lookups to first target the offial
    Maven Central repo. This of course failed at the FINEOS proxy but the number of requests
    per developer/CI coupled with the build time increase means that it is worth protecting
    against this type of thing creeping in to our builds in the future.

    The below closure will remove any non-FINEOS repo from the repository list for all projects
    both in the projects themselves and in their buildScript definitions.

    Known offending plugins:
    de.obqo.gradle:gradle-lesscss-plugin:1.0-1.3.3 @ https://github.com/obecker/gradle-lesscss-plugin/blob/master/src/main/groovy/de/obqo/gradle/lesscss/LessPlugin.groovy
*/

gradle.projectsLoaded { g ->
    g.rootProject.allprojects { project ->
        [buildscript.repositories, repositories].each { repoHandler ->
            repoHandler.matching {!it.url.toString().startsWith("${fineosRepositoryPrefix}") && !"MavenLocal".equals(it.name) && !it.url.toString().startsWith("file:/")}.all { repo ->
                project.logger.warn("*** settings.gradle - Removing repo ${repo.name} ('${repo.url}') from ${project.name}")
                repoHandler.remove(repo)
            }
        }
    }
}